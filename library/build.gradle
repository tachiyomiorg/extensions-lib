apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

version = '1.2'

android {
    compileSdkVersion 27
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    provided "com.squareup.okhttp3:okhttp:3.10.0"
    provided "io.reactivex:rxjava:1.3.6"
    provided "org.jsoup:jsoup:1.10.2"
    provided "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

repositories {
    mavenCentral()
}

ext {
    publishedGroupId = 'eu.kanade.tachiyomi'
    libraryName = 'tachiyomi-extensions-lib'
    artifact = 'extensions-library'

    libraryDescription = 'Tachiyomi library to create new catalogues'

    siteUrl = 'https://github.com/tachiyomiorg/extensions-lib'
    gitUrl = 'https://github.com/tachiyomiorg/extensions-lib.git'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
}

group = publishedGroupId
archivesBaseName = artifact

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'jar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id 'inorichi'
                    name 'Javier Tomas'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task makeJar(type: Jar) {
    from zipTree('build/intermediates/bundles/release/classes.jar')
}

artifacts {
    archives sourcesJar
    archives makeJar
}
